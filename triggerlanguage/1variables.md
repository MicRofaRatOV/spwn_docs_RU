# Переменные

Так как мы не можем вывести в консоль `Hello, world!`, при помощи триггеров в Geometry Dash, мы сразу перейдём к основным функциям языка SPWN, первая из которых **переменные**.

**В SPWN переменные объявляются следующим образом:**

```spwn
// let [имя переменной] = [значение]

let a = 10
let b = 60
let c = a + b
```

При обращении к значениям переменных они будут работать как и в любом другом языке программирования, но если мы пытаемся изменять переменные в SPWN, их возможности будут слегка ограничены. Из этого следует, что большинство переменных в SPWN будут _константами или постоянными_. Для понимания, вы можете объявлять константы следующим образом:

```spwn
// [имя переменной (константы)] = [значение]

a = 10
b = 60
c = a + b
```

?> _**Примечание:** У вас **нету обязательства** использовать использовать в SPWN постоянные, но это будет хорошим тоном в программировании и позволит изменять переменные только тогда, когда это требуется._

?> _**Примечание:** Когда вам понадобится изменяемая переменная, скорее всего, вам понадобится ["счётчик"](triggerlanguage/5counter.md) из стандартной библиотеки, который является оболочкой для системы идентификаторов элементов в Geometry Dash. Об этом будет подробнее через пару страниц._

# Значения в SPWN

Итак, что вы можете добавить в переменные? Довольно много всего.
Некоторые типы данных (и значений) могут быть вам известны из других языков:

```spwn
num = 10                      // Числовая

text = "Hello, I'm a string!" // Строчная

array = [1, 2, 3]             // Массив

boolean = true                // Логическая

dictionary = {                // Словарь
    a: 1,
    b: 2,
    c: 3
}
```

Эти значения можно использовать точно так же, как и в любом другом языке программирования.

Вот некоторые примеры кода:

```spwn
num = -(1000 * 1000) / 10 // -100000

name = "Spu7Nix" // MicRofaRatOV :P
text = "Hello, my name is " + name + "!"

let array = [1, 2, 3]
my_number = array[2] // 3

array.push(4) // после этого массив будет выглядеть так: [1, 2, 3, 4]

boolean = true
boolean2 = !boolean // false или ложь

let dict = {
    a: 1,
    b: 2,
    c: 3
}

a = dict.a        // 1
dict.d = 4        // Добавляет новый ключ "d" с постоянным значением 4 в словарь
let dict.e = 5    // Добавляет новый ключ "e" с переменным значением 5 в словарь
let dict['f'] = 6 // Так же имеется индексация, для добавления в словарь
```

?> _**Примечание:** Чтобы увидеть весь список операторов в SPWN, перейдите [сюда](operators.md)_

## Идентификаторы

В SPWN также есть значения, которые особенно полезны при создании уровней Geometry Dash:

```spwn
group = 10g // group или группа 10
color = 10c // color или цвет 10
item  = 10i // item-ID или идентификатор предмета 10
block = 10b // collision block-ID или идентификатор блока столкновения 10
```

Эти значения называются `ID`s (или же идентификаторы) и они чрезвычайно важны. Каждый раз, когда вы будете применять SPWN, вы будете использовать их для получения результата. Иногда вам потребуется указать конкретный `ID`, например, если вы уже создали игрового персонажа в редакторе и хотите его переместить. Однако, если это не так, и вы будете ссылаться на эти значения только внутри своего кода, вам не нужно знать, какой `ID` вы используете. В таком случае вы сможете использовать обозначение в виде `?`:

```spwn
group = ?g // some group (какая-то группа)
color = ?c // some color (какой-то цвет)
item  = ?i // some item-ID (какой-то идентификатор)
block = ?b // some collision block-ID (какой-то идентификатор блока столкновения)
```

_Вы можете интерпретировать это, как нажатие на кнопку "next free" в редакторе, которая добавляет объект в следующую свободную группу, за исключением того, что вы не знаете её значение_

Это все типы значений, о которых вам надо знать.

[**Следующая страница**](triggerlanguage/2basic_triggers.md)
